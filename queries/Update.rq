PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mf:      <http://www.w3.org/2001/sw/DataAccess/tests/test-manifest#>
PREFIX dawgt:   <http://www.w3.org/2001/sw/DataAccess/tests/test-dawg#>
PREFIX qt:      <http://www.w3.org/2001/sw/DataAccess/tests/test-query#>
PREFIX ut:      <http://www.w3.org/2009/sparql/tests/test-update#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX ent:     <http://www.w3.org/ns/entailment/RDF>
PREFIX rs:      <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>

SELECT DISTINCT ?type ?name ?query ?result ?data ?test ?feature ?comment ?approval ?approvedBy ?regime (GROUP_CONCAT(DISTINCT CONCAT(STR(?actionGraphStore), "%", STR(?actionGraphLabel)); SEPARATOR=";") AS ?graphDatas) (GROUP_CONCAT(DISTINCT CONCAT(STR(?resultGraphStore), "%", STR(?resultGraphLabel)); SEPARATOR=";") AS ?resultgraphDatas) 
WHERE {
    ?test rdf:type mf:UpdateEvaluationTest .
    BIND ("UpdateEvaluationTest" AS ?type) .
    ?test mf:action ?action .
    ?action ut:request ?query .
    OPTIONAL  { ?action ut:data ?data .} 
    OPTIONAL  { 
        ?action ut:graphData ?actionGraphData .
        ?actionGraphData ut:graph ?actionGraphStore .
        ?actionGraphData rdfs:label ?actionGraphLabel .
    }
    OPTIONAL {?action sd:entailmentRegime ?regime .}
    ?test mf:result ?resultObject .
    OPTIONAL  { ?resultObject ut:data ?result } .
    OPTIONAL  { 
        ?resultObject ut:graphData ?graphData .
        ?graphData ut:graph ?resultGraphStore .
        ?graphData rdfs:label ?resultGraphLabel .
    }
    OPTIONAL {?test mf:name ?name }.
    OPTIONAL {?test mf:feature ?feature }.
    OPTIONAL {?test rdfs:comment ?comment }.
    OPTIONAL {?test dawgt:approval ?approval }.
    OPTIONAL {?test dawgt:approvedBy  ?approvedBy }.
}
GROUP BY ?type ?name ?query ?result ?data ?test ?feature ?comment ?approval ?approvedBy ?regime
